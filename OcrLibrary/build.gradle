plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

android {
    compileSdkVersion compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode project_version_code
        versionName project_version_name

        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    flavorDimensions 'ocr_library_types'
    productFlavors {
        cpu {
            minSdkVersion 21
            externalNativeBuild {
                cmake {
                    arguments "-DOCR_LITE_VULKAN=OFF"
                }
            }
        }
        gpu {
            minSdkVersion 24
            externalNativeBuild {
                cmake {
                    arguments "-DOCR_LITE_VULKAN=ON"
                }
            }
        }
    }

    libraryVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = "${project.name}-${defaultConfig.versionName}-${variant.flavorName}-${variant.buildType.name}.aar"
            }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //Android
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
}